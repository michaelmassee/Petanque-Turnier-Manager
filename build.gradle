
plugins {
    id 'java'
    id 'application'
}

group = 'de.petanqueturniermanager'
// Version aus description.xml lesen
def descriptionXml = new groovy.xml.XmlSlurper(false, false).parse(file('description.xml'))
version = descriptionXml.version.@value.text()

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
    withJavadocJar()
    withSourcesJar()
}

repositories {
    mavenCentral()
}

// Configuration properties
def libreOfficePath = findProperty('libreoffice.path') ?: '/usr/lib/libreoffice'
def libreOfficeClassesPath = "${libreOfficePath}/program/classes"
def extensionId = descriptionXml.identifier.@value.text()

configurations {
    libreoffice
}

dependencies {

    implementation 'com.google.guava:guava:33.5.0-jre'
    implementation 'org.apache.commons:commons-lang3:3.20.0'
    implementation 'org.apache.commons:commons-collections4:4.5.0'
    implementation 'org.apache.commons:commons-text:1.15.0'
    implementation 'commons-io:commons-io:2.21.0'
    implementation 'org.apache.logging.log4j:log4j-core:2.25.3'
    implementation 'org.kohsuke:github-api:1.330'
    implementation 'com.google.code.gson:gson:2.13.2'
    implementation 'io.github.g00fy2:versioncompare:1.5.0'
    implementation 'org.jsoup:jsoup:1.22.1'
    implementation 'com.opencsv:opencsv:5.12.0'

    // LibreOffice SDK dependencies - using local installation
    compileOnly fileTree(dir: libreOfficeClassesPath, include: ['*.jar'])
    compileOnly files(
        "${libreOfficeClassesPath}/ridl.jar",
        "${libreOfficeClassesPath}/unoil.jar",
        "${libreOfficeClassesPath}/jurt.jar",
        "${libreOfficeClassesPath}/juh.jar"
    )
    
    // LibreOffice SDK für Tests - use testImplementation so classes are available at runtime
    testImplementation fileTree(dir: libreOfficeClassesPath, include: ['*.jar'])
    testImplementation files(
        "${libreOfficeClassesPath}/ridl.jar",
        "${libreOfficeClassesPath}/unoil.jar",
        "${libreOfficeClassesPath}/jurt.jar",
        "${libreOfficeClassesPath}/juh.jar"
    )

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.assertj:assertj-core:3.27.6'
    testImplementation 'org.mockito:mockito-core:5.21.0'
}

configurations.all {
	exclude group: 'commons-collections', module: 'commons-collections'
}

application {
    mainClass = 'de.petanqueturniermanager.SheetRunner'
}

task cleanCache (type: Delete) {
  delete "dist/.ooo-debug/user/uno_packages/cache"
}

task copyRuntimeLibs(type: Sync) {
    group = 'LibreOffice'
    description = 'Kopiert Runtime-Dependencies für die OXT'
    from configurations.runtimeClasspath
    into "$buildDir/runtime-libs"
}

// Task zum Erstellen des JAR mit UNO Registration
task buildPluginJar(type: Jar) {
    group = 'LibreOffice'
    description = 'Erstellt das JAR mit UNO Registration und Class-Path zu libs'
    dependsOn classes, copyRuntimeLibs
    archiveFileName.set("${project.name}.jar")
    
    from sourceSets.main.output
    
    // UNO components-Datei ins JAR META-INF (LibreOffice Standard-Location)
    from('.') {
        include 'PetanqueTurnierManager.components'
        into 'META-INF'
    }
    
    manifest {
        attributes(
            'RegistrationClassName': 'de.petanqueturniermanager.comp.RegistrationHandler',
            'Class-Path': configurations.runtimeClasspath.collect { 
                "libs/${it.name}" 
            }.join(' ')
        )
    }
}

// Task zum Erstellen der OXT-Datei (LibreOffice Extension)
// IDL zu RDB kompilieren (UNO Type Registry für AddIn-Funktionen)
task compileIdl(type: Exec) {
    group = 'LibreOffice'
    description = 'Kompiliert IDL-Dateien zu einer RDB Type-Registry'
    def unoidlWrite = "${libreOfficePath}/sdk/bin/unoidl-write"
    def typesRdb = "${libreOfficePath}/program/types.rdb"
    def offapiRdb = "${libreOfficePath}/program/types/offapi.rdb"
    def idlFile = file('idl/de/petanqueturniermanager/AddInGlobal.idl')
    def outputRdb = file("$buildDir/rdb/AddInGlobal.rdb")

    inputs.file idlFile
    outputs.file outputRdb

    doFirst {
        outputRdb.parentFile.mkdirs()
    }
    commandLine unoidlWrite, typesRdb, offapiRdb, idlFile, outputRdb
}

task buildOXT(type: Zip) {
    group = 'LibreOffice'
    description = 'Erstellt die OXT-Datei (LibreOffice Extension)'
    dependsOn buildPluginJar, copyRuntimeLibs, compileIdl
    archiveFileName.set("${project.name}-${version}.oxt")
    destinationDirectory.set(file("$buildDir/distributions"))
    
    // JAR-Datei hinzufügen
    from(buildPluginJar.archiveFile) {
        into ''
    }
    
    // META-INF manifest
    from('META-INF') {
        into 'META-INF'
    }
    
    // Description and configuration files
    from('.') {
        include 'description.xml'
    }
    
    from('description') {
        include '*.txt'
        into 'description'
    }
    
    // Registry configuration (XCU files)
    from('registry') {
        into 'registry'
    }
    
    // Images
    from('images') {
        into 'images'
    }
    
    // Runtime dependencies (libs)
    from("$buildDir/runtime-libs") {
        into 'libs'
    }
    
    // IDL files
    from('idl') {
        into 'idl'
    }

    // RDB Type Registry (kompiliert aus IDL)
    from("$buildDir/rdb") {
        include '*.rdb'
    }
}

// Task zum Installieren der Extension in LibreOffice
task installExtension(type: Exec) {
    group = 'LibreOffice'
    description = 'Installiert die erstellte Extension in LibreOffice'
    dependsOn buildOXT
    def oxtFile = "${buildDir}/distributions/${project.name}-${version}.oxt"
    commandLine 'unopkg', 'add', '-f', oxtFile
    
    doFirst {
        println "Installiere Extension: ${oxtFile}"
    }
}

// Task zum Deinstallieren der Extension
task uninstallExtension(type: Exec) {
    group = 'LibreOffice'
    description = 'Deinstalliert die Extension aus LibreOffice'
    commandLine 'unopkg', 'remove', extensionId
    ignoreExitValue = true
    
    doFirst {
        println "Deinstalliere Extension: ${extensionId}"
    }
}

// Task zum Neuinstallieren
task reinstallExtension {
    group = 'LibreOffice'
    description = 'De-/Neuinstallation der Extension'
    dependsOn uninstallExtension, installExtension
    installExtension.mustRunAfter uninstallExtension
}

// Debug-Task: Startet LibreOffice mit Debug-Port
task debugLibreOffice(type: Exec) {
    group = 'LibreOffice'
    description = 'Startet LibreOffice im Debug-Modus mit JDWP'
    dependsOn reinstallExtension
    
    environment 'JAVA_TOOL_OPTIONS', '-agentlib:jdwp=transport=dt_socket,server=y,suspend=y,address=*:5005'
    
    commandLine 'libreoffice', '--calc'
    
    doFirst {
        println "LibreOffice wird im Debug-Modus gestartet..."
        println "Verbinde deinen Debugger mit Port 5005"
    }
}

// Tests benötigen die aktuelle OXT installiert – reinstallExtension baut und installiert sie
test {
    dependsOn reinstallExtension
    // UITests starten LibreOffice auf Port 8100 – nur ein Worker gleichzeitig erlaubt
    maxParallelForks = 1
    // Im Gradle-Build immer headless; manuelle IDE-Starts verwenden headless=false (Standard)
    systemProperty 'uitest.headless', 'true'
}

// Build-Task
task buildPlugin {
    group = 'LibreOffice'
    description = 'High-level Task: Baut die OXT und zeigt Ausgabepfad'
    dependsOn buildOXT

    doLast {
        println "Plugin erfolgreich gebaut: ${buildDir}/distributions/${project.name}-${version}.oxt"
    }
}
